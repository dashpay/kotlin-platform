plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.google.protobuf'
    id('io.gitlab.arturbosch.detekt').version("1.23.7")
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser'
}

group 'org.dashj.platform'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':dash-sdk-java')
    implementation "org.dashj:dashj-core:$dashj_version"
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.74'
    implementation 'org.json:json:20190722'
    implementation 'com.google.guava:guava:30.0-jre'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    implementation 'com.google.protobuf:protobuf-javalite:3.21.7'

    testImplementation 'org.slf4j:slf4j-jdk14:1.7.30'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation 'co.nstant.in:cbor:0.8'
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.4.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"
    implementation 'io.grpc:grpc-okhttp:1.54.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.54.0' // CURRENT_GRPC_VERSION
    implementation('io.grpc:grpc-protobuf-lite:1.54.0') { // CURRENT_GRPC_VERSION
        exclude module: "protobuf-lite"
    }
    implementation 'io.grpc:grpc-netty-shaded:1.54.0'  // replace with the latest version
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.21.7:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.21.7'
        }
    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.54.0:osx-x86_64'
            } else {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.54.0'
            }
        }
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                remove java
                java { option 'lite' }
            }
            task.plugins {
                remove grpc
                grpc { option 'lite' }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
        kotlin {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
        proto {
            srcDirs += 'src/main/proto' // default value
        }
    }
}

test {
    useJUnitPlatform()
    systemProperty "java.library.path", "../dash-sdk-java/build/cmake"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // or DuplicatesStrategy.WARN
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'dash-sdk-kotlin'
            version project.rootProject.ext.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Higher level Dash SDK for Kotlin'
                description = 'High Level Dash SDK Java Library for Platform'
                url = 'httsp://dashj.org'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hashengineering'
                        name = 'HashEngineering'
                        email = 'hashengineeringsolutions@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/HashEngineering/kotlin-platform.git'
                    developerConnection = 'scm:git:ssh://github.com/HashEngineering/kotlin-platform.git'
                    url = 'http://github.com/HashEngineering/kotlin-platform.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    def projectVersion = rootProject.ext.version
    project {
        name = 'dash-sdk-kotlin'
        description = 'Higher level Dash SDK for Kotlin'
        website = 'https://dashj.org'
        authors = ['HashEngineering']
        license = 'The Apache License, Version 2.0'
        gitRootSearch = true
        version = projectVersion
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository("$buildDir/staging-deploy")
                }
            }
            nexus2 {
                app {
                    active = 'SNAPSHOT'
                    url = 'https://central.sonatype.com/repository/maven-snapshots/'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository("$buildDir/staging-deploy")
                }
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

assemble.dependsOn(publishToMavenLocal)
assemble.dependsOn(sourcesJar)
// apply from: 'ktlint.gradle'