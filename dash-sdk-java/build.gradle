plugins {
    id 'java'
    id 'cpp'
    id("dev.welbyseely.gradle-cmake-plugin") version "0.1.0"
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}
apply plugin: 'org.jreleaser'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    testImplementation 'junit:junit:4.13.2'  // Latest JUnit 4
    testImplementation 'org.dashj:dashj-core:21.1.1'
    testImplementation 'com.google.guava:guava:32.0.0-android'
}

cmake {
    sourceFolder=file("$projectDir/src/main/cpp")
}

// TODO: move this to the cmake project
task generateSWIG(type: Exec) {
    // Define the input directory where your .i files are located
    def swigInputDir = "${projectDir}/src/main/swig"

    // Define the output directory for the generated Java and C/C++ files
    def javaOutputDir = "${buildDir}/generated/java/org/dashj/platform/sdk"
    def cppOutputDir = "${buildDir}/generated/cpp"

    // Create the output directories if they don't already exist
    file(javaOutputDir).mkdirs()
    file(cppOutputDir).mkdirs()

    // execute ignore.py
    commandLine 'python3', 'ignore.py'

    // Command to execute SWIG
    // Make sure to adjust the parameters according to your needs
    commandLine 'swig', '-java', '-c++',
            '-outdir', javaOutputDir,
            '-package', 'org.dashj.platform.sdk',
            '-o', "${cppOutputDir}/sdk.cpp",
            '-I${swigInputDir}', // Include directory for SWIG interfaces
            "${swigInputDir}/root.i" // SWIG interface file


    // Ensure that this task is run before compiling the sources
    tasks.compileJava.dependsOn generateSWIG
}

// Ensure that the generated Java sources are included in compilation
sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/java"
        }
    }
}
tasks.named('compileJava').configure { dependsOn 'cmakeBuild' }
//tasks.named('cmakeBuild').configure { dependsOn 'generateSWIG' }
//compileJava.dependsOn cmakeBuild
//cmakeBuild.dependsOn generateSWIG


test {
    //useJUnitPlatform()
    systemProperty "java.library.path", "build/cmake"
    println(systemProperties["java.library.path"])
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

// Create a separate test JAR
task testJar(type: Jar) {
    archiveClassifier.set("tests") // This adds '-tests' to the name of the JAR
    from sourceSets.test.output  // Package the test classes
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives testJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'dash-sdk-java'
            version project.rootProject.ext.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact(testJar) {
                classifier "tests"
            }
            pom {
                name = 'Dash SDK for Java'
                description = 'Dash SDK Java Library for Platform'
                url = 'httsp://dashj.org'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hashengineering'
                        name = 'HashEngineering'
                        email = 'hashengineeringsolutions@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/HashEngineering/kotlin-platform.git'
                    developerConnection = 'scm:git:ssh://github.com/HashEngineering/kotlin-platform.git'
                    url = 'http://github.com/HashEngineering/kotlin-platform.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    def projectVersion = rootProject.ext.version
    project {
        name = 'dash-sdk-java'
        description = 'Dash SDK for Java'
        website = 'https://dashj.org'
        authors = ['HashEngineering']
        license = 'MIT'
        gitRootSearch = true
        version = projectVersion
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository("$buildDir/staging-deploy")
                }
            }
            nexus2 {
                app {
                    active = 'SNAPSHOT'
                    url = 'https://central.sonatype.com/repository/maven-snapshots/'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository("$buildDir/staging-deploy")
                }
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

assemble.dependsOn(publishToMavenLocal)
assemble.dependsOn(sourcesJar)

// jacocoTestReport {
//     dependsOn test // tests are required to run before generating the report
//     reports {
//         xml.enabled true
//         xml.destination file("${project.projectDir}/build/reports/jacoco/jacoco.xml")
//     }
// }